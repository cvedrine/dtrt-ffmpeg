#! /usr/bin/zsh

#SBATCH -p publicgpu   # utiliser la file hpc standard
#xxxSBATCH -N 1-1         # nombre de noeuds min-max
#SBATCH -t 24:00:00 # temps estimé utilisépar le scheduler slurm
#SBATCH --gres=gpu:1   # on exige 1 GPU

. pod-ssh.zsh
. dtrt-ffmpeg.zsh

DEBUG () { print -lu2 "$@" }

id=$1
remote=$( ssh pod get_video_path $id )

DEBUG "preparing video $1 from $remote"

file=$remote:t
ssh pod cat_video $id > $file

rm -rf videos
mkdir -p videos
cd videos

case "$file:e" {
    (wav|mp3|ogg)
        dest=$file:t:r.mp3
        # ffmpeg -i ../$file -ar 48000 -ac 2 -b:a 128k $dest
        lame ../$file -b 128k $dest
        ssh pod add_mp3_for $id $dest
        exit
    ;;
}

DEBUG "dtrt-ffmpeg-overview  ../$file:t"
{ dtrt-ffmpeg-overview  ../$file:t
    add_overview_for $id overview.png } &

DEBUG "dtrt-ffmpeg-thumbnails  ../$file:t"
{ dtrt-ffmpeg-thumbnails  ../$file:t
    set_thumbnails_for $id  thumbnails/{3,6,9}0.png } &

#TODO: blinder en testant l'extention et le mime type
#(un jour)

DEBUG "dtrt-ffmpeg-downscale ../$file:t"

dtrt-ffmpeg-downscale ../$file:t
< downscaled while {read height result} {
    add_encoding_for $id $height $result &
}

ssh pod setattr $id duration $(
        video_info_get ../$file duration |
        sed 1q |
        cut -f1 -d. )

wait

ssh pod setattr $id encoding_status "'DONE $(date)'"

