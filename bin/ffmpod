#! /usr/bin/zsh

#SBATCH -p publicgpu   # utiliser la file hpc standard
#SBATCH -N 1-1         # nombre de noeuds min-max
#SBATCH -t 24:00:00 # temps estimé utilisépar le scheduler slurm
#SBATCH --gres=gpu:1   # on exige 1 GPU

scp navigator.py potest:p/pods/management/commands/
scp restricted-shell potest:

KEY=( django(:A) )
pod () { ssh -oIdentitiesOnly=yes -i $KEY potest "$@" }

add_encoding_for   () { pod add_encoding_for "${(q)@}" < $3 }
add_overview_for   () { pod add_overview_for "${(q)@}" < $2 }
set_thumbnails_for () {
    tar cf - "${(@)argv[2,5]}" |
    ssh -oIdentitiesOnly=yes -i $KEY potest set_thumbnails_for $@ }

id=$1
remote=$( pod get_video_path $id )
file=$remote:t
pod cat_video $id > $file
rm -rf videos
mkdir -p videos
cd videos

{ dtrt-ffmpeg-overview  ../$file:t &> OVER
    add_overview_for $id overview.png
    set_thumbnails_for $id  strips.*/20.png strips.*/50.png strips.*/70.png } &

# [[ $input:e = *.(wav|mp3|ogg|flac) ]] && {
# 	dest=$input:t.mp3
# 	ffmpeg -i $input -ar 48000 -ac 2  -a:b 128k $dest
# 	# scp $dest $store
# 	# TODO: pod POST le resultat
# 	exit
# }

#TODO: blinder en testant l'extention et le mime type
#(un jour)

dtrt-ffmpeg-downscale ../$file:t &> DOWN
< downscaled while {read height result} {
    add_encoding_for $id $height $result &
}

wait
print -u2 done
