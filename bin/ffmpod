#! /usr/bin/zsh

#SBATCH -p publicgpu   # utiliser la file hpc standard
#xxxSBATCH -N 1-1         # nombre de noeuds min-max
#SBATCH -t 24:00:00 # temps estimé utilisépar le scheduler slurm
#SBATCH --gres=gpu:1   # on exige 1 GPU

. pod-ssh.zsh
. dtrt-ffmpeg.zsh

DEBUG () { print -lu2 "$@" }

id=$1
remote=$( ssh pod get_video_path $id )

DEBUG "preparing video $1 from $remote"

file=$remote:t
ssh pod cat_video $id > $file

rm -rf videos
mkdir -p videos
cd videos

case "$file:e" {
    (wav|mp3|ogg|wma)
        dest=$file:t:r.mp3
        command=( ffmpeg -i ../$file -ar 48000 -ac 2 -b:a 128k $dest )
        # command=( lame ../$file -b 128k $dest)
        print -l $command > command
        $command
        duration=$( info_get $dest a duration | keep_one_int )
        add_mp3_for $id $dest
        print ssh pod setattr $id duration $duration
        ssh pod setattr $id duration $duration
        ssh pod setattr $id encoding_status "'DONE $(date)'"
        ssh pod disable $id encoding_in_progress
        exit
    ;;
}

DEBUG "dtrt-ffmpeg-overview  ../$file:t"
{ dtrt-ffmpeg-overview  ../$file:t
    add_overview_for $id overview.png } &

DEBUG "dtrt-ffmpeg-thumbnails  ../$file:t"
{ dtrt-ffmpeg-thumbnails  ../$file:t
    set_thumbnails_for $id  thumbnails/{3,6,9}0.png } &

#TODO: blinder en testant l'extention et le mime type
#(un jour)

DEBUG "dtrt-ffmpeg-downscale ../$file:t"

dtrt-ffmpeg-downscale ../$file:t
< downscaled while {read height result} {
    : ${duration:=$( info_get $result v duration | keep_one_int )}
    add_encoding_for $id $height $result &
}

print duration: $duration
ssh pod setattr $id duration $duration
ssh pod setattr $id encoding_status "'DONE $(date)'"
ssh pod disable $id encoding_in_progress
wait

#Cleaning folders on HPC
cd
daysafter=1     #How many days after last modification
if [ $(whoami) = "pod" ]
then daysafter=7	#Seven days after in production
fi
find ./pod.* -maxdepth 0 -mtime $daysafter -print0 | xargs -0 rm -rf      #Delete old working folders

